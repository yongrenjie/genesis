; jy-noah3_SpSpT

;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

define delay DC_SpB1
define delay DC_SpB2
define delay DC_SpB3
define delay DC_SpB4
define delay DC_SpB5
define delay DC_SpB6
define delay DC_SpB7
define delay DC_SpB8
define delay DC_SpB9
define delay DN_SpB1
define delay DN_SpB2
define delay DN_SpB3
define delay DN_SpB4
define delay DN_SpB5
define delay DN_SpB6
define delay DN_SpB7

"FACTOR1= (d9/(p6*115.112))/2"
"d0     = 3u"                       ; 13C HSQC t1
"d2     = 0.5s/cnst2"               ; JCOMP
"d4     = 0.25s/cnst2"              ; 13C INEPT
"d10    = 3u"                       ; TOCSY t1
"d20    = 3u"                       ; 15N HSQC t1/2
"d24    = 0.25s/cnst4"              ; 15N INEPT
"in0    = inf1/2"                   ; 13C HSQC increment
"in10   = 2*dw"                     ; TOCSY increment
"in20   = 1000000/(2*cnst40*sfo3)"  ; 15N HSQC increment: cnst40 = SW(15N)
"l1     = 0"                        ; Running counter of total t1 increments for 13C HSQC
"l3     = FACTOR1*2"                ; number of TOCSY loops
"p2     = p1*2"                     ; 1H hard 180
"p4     = p3*2"                     ; 13C hard 180
"p17    = p16*cnst16"               ; Longer gradients for 15N seHSQC v2
"p22    = p21*2"                    ; 15N hard 180
"DC_SpB1 = d4-p14/2"                       ; zz-filter
"DC_SpB2 = d4+p14/2"                       ; zz-filter
"DC_SpB3 = d4-larger(p2,p14)/2"            ; INEPT
"DC_SpB4 = d2-p16-d16-p2-d0*2-p3*2/PI"     ; 13C editing period
"DC_SpB5 = d2-p2+p3*2/PI"                  ; 13C editing period
"DC_SpB6 = p16+d16+p2+d0*2-4u"             ; 13C post-t1, if no editing
"DC_SpB7 = d6-cnst17*p24/2-p19-d16"        ; first spin echo after t1
"DC_SpB8 = d4-larger(p2,p14)/2-p16-d16"    ; second spin echo after t1
"DC_SpB9 = p16+d16-p1*0.78+de+8u"          ; final spin echo for refocusing gradient
"DN_SpB1 = d24-p22/2"                     ; zz-filter
"DN_SpB2 = d24+p22/2"                     ; zz-filter
"DN_SpB3 = d24-larger(p2,p22)/2"          ; INEPT
"DN_SpB4 = p17+d16+p2+d0*2-4u"            ; 15N post-t1, if no editing
"DN_SpB5 = d26-larger(p2,p22)/2-p19-d16"  ; first spin echo after t1
"DN_SpB6 = d24-larger(p2,p22)/2-p16-d16"  ; second spin echo after t1
"DN_SpB7 = p17+d16-p1*0.78+de+8u"         ; final spin echo for refocusing gradient
"l0     = td1/(nbl*2)"              ; Total number of t1 increments for 13C HSQC
"l1     = 0"                        ; Running counter of total t1 increments for 13C HSQC
"acqt0=0"
baseopt_echo

1 ze
2 30m
3 5m do:f2
4 50u UNBLKGRAD
  ; Cleanup
  4u pl3:f3
  4u pl2:f2
  (p21 ph1):f3
  (p3 ph1):f2
  4u pl1:f1
  p16:gp0
  4u
  (p1 ph1)
  4u
  p16:gp0*-1.37
  4u
  (p1 ph2)
  4u
  p16:gp0*0.77
  50u BLKGRAD
  d1 st0
  50u UNBLKGRAD

  ; MODULE 1
  ; 15N-1H seHSQC version 2

  ; reverse zz-filter
  (p1 ph1):f1
  DN_SpB1
  (p22 ph1):f3
  (p2 ph1):f1
  DN_SpB2
  (p1 ph1):f1
  DN_SpB1
  (p22 ph1):f3
  (p2 ph1):f1
  DN_SpB2            ; 15N-1H: y,  X-1H: z

  ; forward INEPT
  (p1 ph24):f1
  DN_SpB3
  4u
  (center (p2 ph1):f1 (p22 ph1):f3 )
  4u
  DN_SpB3
  (p28 ph1):f1   ; trim pulse 1H
  4u
  (p1 ph2):f1
  (p21 ph20):f3

  ; t1 evolution
  d20
  (p2 ph22):f1
  d20
  p17:gp21*EA
  d16
  (p22 ph1):f3
  4u
  DN_SpB4

  ; reverse INEPT for first component
  (center (p1 ph1):f1 (p21 ph22):f3 )
  p19:gp23
  d16
  DN_SpB5
  (center (p2 ph1):f1 (p22 ph1):f3 )
  DN_SpB5
  p19:gp23
  d16
  (center (p1 ph2):f1 (p21 ph23):f3 )

  ; reverse INEPT for second component
  p16:gp24
  d16
  DN_SpB6
  (center (p2 ph1):f1 (p22 ph1):f3 )
  DN_SpB6
  p16:gp24
  d16
  (p1 ph1):f1

  ; spin echo for refocusing gradient
  DN_SpB7
  (p2 ph1):f1
  4u
  p17:gp22
  d16 pl16:f3
  4u
  goscnp ph28 cpd3:f3
  50u do:f3


  ; Cleanup
  4u pl3:f3
  4u pl2:f2
  (p21 ph1):f3
  (p3 ph1):f2
  50u
  p16:gp0*1.77
  2m st

  ; MODULE 2
  ; 13C-1H seHSQC version 2

  ; reverse zz-filter
  (p1 ph1):f1
  DC_SpB1
  (p14:sp3 ph1):f2
  (p2 ph1):f1
  DC_SpB2
  (p1 ph1):f1
  DC_SpB1
  (p14:sp3 ph1):f2
  (p2 ph1):f1
  DC_SpB2            ; 13C-1H: y,  12C-1H: z

  ; forward INEPT
#ifdef EDIT
  (p1 ph2):f1
#else
  (p1 ph9):f1
#endif
  DC_SpB3
  4u
  (center (p2 ph1):f1 (p14:sp3 ph1):f2 )
  4u
  DC_SpB3 pl2:f2
  (p28 ph1):f1   ; trim pulse 1H
  4u
  (p1 ph2):f1 (p3 ph3):f2

  ; t1 evolution
  d0
  (p2 ph5):f1
  d0

  ; optional multiplicity editing
  ; edited part from hsqcedetgpsisp2.3
  ; nonedited part from hsqcetgpsisp2.2
#ifdef EDIT
  p16:gp1*EA
  d16
  DC_SpB4
  (p31:sp18 ph1):f2
  (p2 ph1):f1
  DC_SpB5
  4u
  (p31:sp18 ph1):f2
  2u
  2u pl2:f2
#else
  p16:gp1*EA
  d16
  (p24:sp7 ph1):f2
  4u
  DC_SpB6 pl2:f2
#endif

  ; reverse INEPT for first component
  (center (p1 ph1):f1 (p3 ph5):f2 )
  p19:gp3
  d16
  DC_SpB7
  (center (p2 ph1):f1 (p24:sp7 ph1):f2 )
  DC_SpB7
  p19:gp3
  d16 pl2:f2
#ifdef EDIT
  (center (p1 ph2):f1 (p3 ph7):f2 )
#else
  (center (p1 ph2):f1 (p3 ph6):f2 )
#endif

  ; reverse INEPT for second component
  p16:gp4
  d16
  DC_SpB8
  (center (p2 ph1):f1 (p14:sp3 ph1):f2 )
  DC_SpB8
  p16:gp4
  d16
  (p1 ph1):f1

  ; spin echo for refocusing gradient
  DC_SpB9
  (p2 ph1):f1
  4u
  p16:gp2
  d16 pl12:f2
  4u
  goscnp ph29 cpd2:f2   ; acquire 13C HSQC
  50u do:f2


  ; Purging
  4u pl2:f2
  (p3 ph1):f2
  50u
  p16:gp0*2.32
  2m st

  ; MODULE 3
  ; TOCSY

  "cnst9  = l3*p6*115.112/1000000"  ; actual TOCSY mixing time
  "cnst9  = cnst9"                  ; force display in ased
  p1 ph13
  d10
  p1 ph1
  10u gron13 pl0:f1
  (p32:sp29 ph1):f1
  20u groff
  d16 pl10:f1
  
						;begin DIPSI2
8 p6*3.556 ph17
  p6*4.556 ph18
  p6*3.222 ph17
  p6*3.167 ph18
  p6*0.333 ph17
  p6*2.722 ph18
  p6*4.167 ph17
  p6*2.944 ph18
  p6*4.111 ph17
  
  p6*3.556 ph18
  p6*4.556 ph17
  p6*3.222 ph18
  p6*3.167 ph17
  p6*0.333 ph18
  p6*2.722 ph17
  p6*4.167 ph18
  p6*2.944 ph17
  p6*4.111 ph18

  p6*3.556 ph18
  p6*4.556 ph17
  p6*3.222 ph18
  p6*3.167 ph17
  p6*0.333 ph18
  p6*2.722 ph17
  p6*4.167 ph18
  p6*2.944 ph17
  p6*4.111 ph18

  p6*3.556 ph17
  p6*4.556 ph18
  p6*3.222 ph17
  p6*3.167 ph18
  p6*0.333 ph17
  p6*2.722 ph18
  p6*4.167 ph17
  p6*2.944 ph18
  p6*4.111 ph17
  lo to 8 times l3
						;end DIPSI2
  p16:gp14
  d16
  10u gron13*1.333 pl0:f1 
  (p32*0.75:sp29 ph1):f1
  20u groff
  d16 pl1:f1
  4u
  p1 ph1

  go=2 ph30


  ; echo/antiecho loop
  1m igrad EA
  1m ip23*2
  1m ip6*2
  1m ip7*2
  1m ip13
  30m wr #0 if #0 zd
  lo to 3 times 2

  ; 13C t1 incrementation loop
  1m id0
  1m ip3*2
  1m ip29*2
  1m rp13    ; I think change this to ip13 for States-TPPI.
  1m id10
  ; 1m ip30*2  ; uncomment for States-TPPI

  ; 15N t1 incrementation check
  "l1 = l1 + 1"
if "l1 % cnst39 == 0"
{
  1m id20
  1m ip20*2
  1m ip28*2
}
  lo to 4 times l0

50u BLKGRAD
exit


ph1=0
ph2=1
ph20=0 2     ; coherence transfer H->N
ph22=0 0 2 2
ph23=1 1 3 3 ; sensitivity enhancement 90 without editing
ph24=3
ph28=0 2 2 0
ph3=0 2     ; coherence transfer H->C
ph5=0 0 2 2
ph6=1 1 3 3 ; sensitivity enhancement 90 without editing
ph7=3 3 1 1 ; sensitivity enhancement 90 with editing
ph9=3
ph29=0 2 2 0
ph13=0 2
ph17=3
ph18=1
ph30=0 2



;gpz1: 80%
;gpz2: 20.1%
;gpz3: 11%
;gpz4: -5%
;gpz13: 14%
;gpz14: 20%
;gpz21: 80%
;gpz22: 8.1%
;gpz23: 11%
;gpz24: -5%
;gpnam0: SMSQ10.100
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam13: SMSQ10.100
;gpnam14: SMSQ10.100
;gpnam21: SMSQ10.100
;gpnam22: SMSQ10.100
;gpnam23: SMSQ10.100
;gpnam24: SMSQ10.100

;sp18:wvm:wu180Jcomp: cawurst-40(280 ppm; Jcomp, L2H)
;sp3:wvm:wu180C13: cawurst-20(60 kHz, 0.5 ms; L2H)
;cpd2:wvm:wudec: cawurst_d-20(220 ppm, 1.4 ms; L2H)


;cnst2: = 1J(CH)
;cnst4: = 1J(NH)
;cnst9: actual TOCSY mixing time (display only)
;cnst16: 15N CTP gradient lengthening factor
;cnst17: = -0.5 for Crp60comp.4
;cnst39: 15N HSQC sensitivity factor
;cnst40: 15N SW (ppm)
;d0: 13C t1
;d1: relaxation delay
;d2: 1/2J(CH)
;d4: 1/4J(CH)
;d6: 1/8J(CH) for all multiplicities, 1/4J(CH) for CH only
;d9: TOCSY mixing time
;d10: 1H t1
;d16: delay for homospoil/gradient recovery [200 us]
;d20: 15N t1
;d24: 1/4J(NH)
;d26: 1/8J(NH) for all multiplicities, 1/4J(NH) for NH only
;l3: number of DIPSI-2 cycles
;p1: f1 channel -  90 degree high power pulse
;p2: f1 channel - 180 degree high power pulse
;p3: f2 channel -  90 degree high power pulse
;p4: f2 channel - 180 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse for inversion
;p16: gradient pulse   [1 ms]
;p17: extended gradient pulse for 15N HSQC
;p19: gradient pulse 2 [600 us]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p24: f2 channel - 180 degree shaped pulse for refocussing
;p31: f2 channel - 180 degree shaped pulse for inversion with J-compensation
;p32: f1 channel - 180 degree shaped pulse (adiabatic)      [20 msec]
;pl1: f1 channel - power level for pulse (default)
;pl2: f2 channel - power level for pulse (default)
;pl3: f3 channel - power level for pulse (default)
;pl10: f1 channel - power level for TOCSY-spinlock
;pl12: f2 channel - power level for CPD/BB decoupling
;spnam3: Crp60,0.5,20.1
;spnam7: Crp60comp.4
;spnam18: Crp60_xfilt.2

